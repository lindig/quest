%---------%---------%---------%---------%---------%---------%---------%---------

\section{Pretty Printer for C}

This module provides pretty printers for our abstract C syntax that
return abstract documents of type [[Pretty.t]]. Such a document can be
formatted for different line length. 

Note that not every syntactically possible abstract syntax is legal.
When a pretty printer detects such a construct, it raises [[Failure]].

<<ppcee.mli>>=
val ty:         Cee.ty          -> Pretty.t     (* type *)
val decl:       Cee.decl        -> Pretty.t     (* declaration *)
val expr:       Cee.expr        -> Pretty.t     (* expression  *)
val init:       Cee.init        -> Pretty.t     (* initializer *)
val stmts:      Cee.stmt list   -> Pretty.t     (* statements  *)
val fundef:     Cee.fundef      -> Pretty.t     (* function definition *)
val program:    Cee.program     -> Pretty.t     (* compilation unit *)
@

\implementation{Implementation}

<<ppcee.ml>>=
module P = Pretty
module C = Cee

<<Helper functions>>
<<Type and Variable Declarations>>
<<Expressions>>
<<Statements>>
<<Function Definitions>>
<<Compilation Unit>>
@

\paragraph{Helper functions}

<<Helper functions>>=
let (@@) f x    = f x
let (@<<) f g x  = f (g x)

let group       = P.group 
let nest        = P.nest 4
let break       = P.break
let empty       = P.empty
let text        = P.text
let (^^)        = P.cat
let (^+) x y    = x ^^ nest (break ^^ y)                  
let (^^^) x y   =      if x = empty then y 
                  else if y = empty then x 
                  else x ^^ break ^^ y


let commabreak      = text "," ^^ break
let semibreak       = text ";" ^^ break
let assign          = text "="
let semi            = text ";"
@

Function [[list]] formats each element of a list [[xs]] using [[f]] and
puts [[sep]] between them. By contrast, [[inter]] puts [[sep]] after
each element.

<<Helper functions>>=
let rec list sep f xs =
    let rec loop acc = function
        | []    -> acc
        | [x]   -> acc ^^ f x 
        | x::y::xs -> loop (acc ^^ f x ^^ sep) (y::xs)
    in
        loop empty xs 

let rec inter sep f xs =
    let rec loop acc = function
        | []    -> acc
        | x::xs -> loop (acc ^^ f x ^^ sep) xs
    in
        loop empty xs
@

Functions to bracket a document. The primed functions create a simpler
layout than the primed functions.

<<Helper functions>>=
let bracket  l x r  = group (text l ^^ nest (break ^^ x) ^^ break ^^ text r)
let bracket' l x r  = group (text l ^^ nest x ^^ text r)
let brace    x      = bracket  "{" x "}" 
let brace'   x      = bracket' "{" x "}" 
let parent   x      = bracket' "(" x ")"
let bracket  x      = bracket  "[" x "]"
let bracket' x      = bracket' "[" x "]"
@

\paragraph{Type- and Variable Declarations}

<<Type and Variable Declarations>>=
let int_width = function
    | C.Short    -> text "short" 
    | C.Long     -> text "long" 
    | C.LongLong -> text "long long"
    | C.IInt     -> empty
    | C.Char     -> failwith "iwidth: called with Char"

let bit_width n = P.printf ":%d" n

let int_sign = function
    | C.Signed   -> empty
    | C.Unsigned -> text "unsigned" 

let bit_sign = function
    | C.Signed   -> text "signed"
    | C.Unsigned -> text "unsigned" 

@

<<Type and Variable Declarations>>=
let rec declaration name t =
    let rec decl t d = match t with
        | C.Void ->                 text "void" ^^^ d
        | C.TyName(s) ->            text s ^^^ d
        | C.Bitfield(w,s) ->        bit_sign s ^^^ d ^^ bit_width w
        | C.Int(C.Char,s) ->        int_sign s ^^^ text "char" ^^^ d
        | C.Int(w,s) ->             int_sign s ^^^ int_width w 
                                               ^^^ text "int" ^^^ d
        | C.Float(C.FFloat) ->      text "float" ^^^ d
        | C.Float(C.Double) ->      text "double" ^^^ d
        | C.Float(C.LongDouble) ->  text "long double" ^^^ d
        | C.Ptr(C.Array(_) as ty)-> decl ty (text "(*" ^^ d ^^ text ")") 
        | C.Ptr(C.Fun(_) as ty) ->  decl ty (text "(*" ^^ d ^^ text ")") 
        | C.Ptr(ty) ->              decl ty (text "*" ^^ d)
        | C.Array(ty,size) ->       decl ty (d ^^ P.printf "[%d]" size)
        | C.Struct(n,None) ->       group (text "struct" ^^^ text n) ^+ d
        | C.Struct(n,Some ms) ->    group (text "struct" ^^^text n) 
                                        ^^^ compound ms 
                                        ^^^ d
        | C.Union(n,None) ->        group (text "union" ^^^ text n) ^+ d
        | C.Union(n,Some ms) ->     group (text "union" ^^^ text n) 
                                        ^^^ compound ms 
                                        ^^^ d
        | C.Fun(fty) ->     
            let ts'  = List.map (fun (name,t) -> ty t) fty.C.args 
                @ if C.variadic fty then [text "..."] else [] in
            let id x = x in
                group (decl fty.C.return d) ^^ parent (list commabreak id ts') 
    in                                      
        group (decl t name)

and compound members =
    let decl doc (name,ty) = doc ^^^ declaration (text name) ty ^^ semi
    in
        brace @@ List.fold_left decl P.empty members

and ty t = declaration P.empty t
@

\paragraph{Expressions and Initializers}

<<Expressions>>=
let infix = function
    | C.Eq        -> "=="
    | C.Neq       -> "!="
    | C.Add       -> "+"
    | C.Mult      -> "*"
    
let prefix = function
    | C.Not       -> "!"

let postfix = function
    | C.PostDec   -> "--"
    | C.PostInc   -> "++"
    
@

Associativity and precedence of expressions according to [Harbison and
Steele, \emph{C---A Reference Manual}, 5th edition, 2002, Prentice
Hall]. A high number denotes a high precedence.

The [[Type]] case was introduced for a special case: the [[va_arg]]
macro for accessing a var arg parameter requires as an argument a type.
We like to reat this macro call like a function call and thus need treat
a type like an expression. Is there a better way?



<<Expressions>>=
type precedence = int 
type side       = Left | Right
let precedence = function
    | C.ID(_)               -> Left, 16
    | C.IntConst(_)         -> Left, 16
    | C.Const(_)            -> Left, 16
    | C.Call(_,_)           -> Left, 16
    | C.Select(_,_)         -> Left, 16
    | C.Access(_,_)         -> Left, 16
    | C.Arrow(_,_)          -> Left, 16
    | C.Deref(_)            -> Right,15
    | C.AddrOf(_)           -> Right,15
    | C.Prefix(C.Not,_)     -> Right,15
    | C.Postfix(_,C.PostDec)
    | C.Postfix(_,C.PostInc)-> Right,15
    | C.Infix(_,C.Add,_)    -> Left, 12
    | C.Infix(_,C.Mult,_)   -> Left, 13
    | C.Cast(_,_)           -> Right,14
    | C.Infix(_,C.Eq,_)  
    | C.Infix(_,C.Neq,_)    -> Left,  9
    | C.Assign(_,_)         -> Right, 2
    (* special cases from here down *)
    | C.Type(_)             -> Right, 1
@

The [[noparens]] predicate is [[true]], if an inner expression requires
no parentheses because of its precedence and assciativity relative to an
outer expression. In the arguments, [[i]] stands for inner, [[o]] for
outer, [[a]] for associativity ([[Left]], [[Right]]), and [[p]] for
precedence. The [[side]] is [[Left]], if the inner expression is a left
(or only) child of the outer expression, and [[Right]] otherwise.

<<Expressions>>=
let noparens (ia,ip) (oa,op) side =
     ip > op
  || ip = op && ia = oa && oa = side
  
let rec expr e =  
    let rec exp outer side e = 
        let inner = precedence e in
        let doc   = match e with 
            | C.ID x          -> text x
            | C.Const x       -> text x
            | C.IntConst i    -> text (string_of_int i)
            | C.Call(f,args)  -> exp inner Left f
                                    ^^ nest (parent @@ exprs args)
            | C.Assign(l,r)   -> exp inner Left l 
                                    ^^^ text "=" 
                                    ^^^ exp inner Right r
            | C.Infix(l,op,r) -> exp inner Left l 
                                    ^^^ text (infix op)
                                    ^^^ exp inner Right r
            | C.Postfix(e,op) -> exp inner Left e ^^ text (postfix op)
            | C.Cast(t,e)     -> parent (ty @@ C.strip t) ^^^ expr e
            | C.Select(e,n)   -> exp inner Left e ^^ text ("." ^ n)
            | C.Access(e,i)   -> exp inner Left e ^^ bracket' (expr i) 
            | C.Type(t)       -> ty (C.strip t)  
            | _               -> failwith "expr: not implemented" 
        in
            if noparens inner outer side 
            then group doc 
            else group @@ parent @@ doc
    in
        exp (Left,0) Left e

and exprs xs = list commabreak expr xs    

let rec init = function
    | C.SingleInit e    -> expr e
    | C.CompoundInit is -> brace' (list commabreak init is) 

let decl d = 
    let doc = function
    | C.VarDecl(name, ty, None) -> declaration (text name) ty
    | C.Typedef(name, ty)       -> text "typedef" ^^^ declaration (text name) ty
    | C.VarDecl(name, ty, Some i) -> 
        declaration (text name) ty ^+ group (assign ^^^ init i)
    in 
        group @@ doc d

let decls dd = group (inter semibreak decl dd)
@    

\paragraph{Statements}

<<Statements>>=
let rec stmt = function
    | C.Expr e          -> expr e ^^ semi
    | C.Block(dd, ss)   -> brace (decls dd ^^^ stmts ss) 
    | C.Return(Some e)  -> text "return" ^^^ expr e ^^ semi
    | C.Return(None)    -> text "return" ^^ semi
    | C.IfThen(e,s)     -> text "if (" ^^ expr e ^^ text ")" ^+ group (stmt s)
    | C.Comment(str)    -> text ("/* "^str^" */")
    | C.Nop             -> empty
    | _                 -> failwith "stmt: not implemented"

and stmts ss = group (list break (group @<< stmt) ss)
@

\paragraph{Function Definition}

<<Function Definitions>>=
let fundef d =
    let static  = function true -> text "static" | false -> P.empty in
    let formals = d.C.ty.C.args in
    let vargs   = ( match d.C.ty.C.varargs with
                  | [] -> P.empty
                  | _  -> commabreak ^^ text "..."
                  ) in
    let formal (n,t) = group (declaration (text n) t) in
        group begin
            group (static d.C.static ^^^ ty d.C.ty.C.return ^^^ text d.C.name 
               ^^ parent (list commabreak formal formals ^^ vargs))
        ^^^  (brace (decls d.C.decls ^^^ stmts d.C.body)) 
        end
@

\paragraph{Compilation Unit}

<<Compilation Unit>>=
let scope = function
    | C.Public -> P.empty
    | C.Extern -> text "extern"
    | C.Static -> text "static"
    
let toplevel = function
    | C.TopDecl(s,d) -> scope s ^+ decl d ^^ semi
    | C.Function(f)  -> fundef f
    | C.CPP(s)       -> text (s^"\n")    (* hack *)

   

let program tls = 
    let f doc x = doc ^^ break ^^ group (toplevel x) in
    List.fold_left f P.empty tls ^^ break  
@
